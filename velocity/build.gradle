plugins {
    id 'java'
}

project.archivesBaseName = project.archives_base_name
project.version = project.version_base

repositories {
    mavenCentral()

    maven {
        name = 'velocitypowered-repo'
        url = 'https://nexus.velocitypowered.com/repository/maven-public/'
    }
}

configurations {
    compileModule

    compileClasspath.extendsFrom compileModule
    runtimeClasspath.extendsFrom compileModule
}

dependencies {

    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'

    compileModule(project(':emotesAPI')){ transitive = false }
    compileModule(project(':executor')){ transitive = false }
    compileModule(project(':emotesServer')){ transitive = false }
    compileModule(project(':emotesAssets')) { transitive = false }

    compileModule("dev.kosmx.player-anim:anim-core:${project.player_anim_core}"){ transitive = false }
}

compileJava {
    dependsOn(':emotesAssets:publicationJar')
}

jar {
    from {
        configurations.compileModule.collect() { it.isDirectory() ? it : zipTree(it) }
    }
    archiveClassifier.set("velocity")
}

tasks.register('copyArtifacts') {
    dependsOn('build')
    doLast {
        copy {
            from "${project.buildDir}/libs/${project.archivesBaseName}-${project.version}-velocity.jar"
            into "${rootProject.projectDir}/artifacts"
        }
    }
}

